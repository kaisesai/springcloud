plugins {
    // 指定插件 不应用在此项目 apply false
    id "org.springframework.boot" version "2.1.5.RELEASE" apply false
}

// 配置所有的子项目信息
subprojects {
    // 应用插件
//    apply plugin: 'idea'
    apply plugin: 'java'
    apply plugin: "org.springframework.boot"
    apply plugin: 'io.spring.dependency-management'

    // 指定JDK版本
    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    version '1.0-SNAPSHOT'
    group 'com.look'

    ext {
//        set('springBootVersion', "2.1.5.RELEASE")
        set('springCloudVersion', "Greenwich.SR1")
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
        all*.exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
    }

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://repo.spring.io/milestone" }
    }

    dependencies {
//        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        // 配置 log4j2 日志
        implementation 'org.springframework.boot:spring-boot-starter-log4j2'
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }

}
